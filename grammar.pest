// whitespace 
WHITESPACE = _{ " " | "\n" | "\r\n" | "\t" }

// tokens
Alpha = @{ 'a'..'z' | 'A'..'Z' }
Digit = @{ '0'..'9' }
Plus = @{ "+" }
Minus = @{ "-" }
Mult = @{ "*" }
Div = @{ "/" }
PowerSign = @{ "^" }
LParen = @{ "(" }
RParen = @{ ")" }
Underscore = @{ "_" }
LBracket = @{ "{" }
RBracket = @{ "}" }
ListSep = @{ "," }
LArray = @{ "[" }
RArray = @{ "]" }
SumOf = @ { "sumof" }
ProductOf = @ { "productof" }

// identifiers and constants
Identifier = @{ (Underscore | Alpha){1} ~ (Alpha | Digit)* }
NumericConstant = @{ Digit+ }

// strings (fucking complicated)
String = ${ "\"" ~ Inner ~ "\"" }
Inner = @{ Char* }
Char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// types
Int8 = @{"int8"}
UInt8 = @{"uint8"}
Int16 = @{"int16"}
UInt16 = @{"uint16"}
Int32 = @{"int32"}
UInt32 = @{"uint32"}
Int64 = @{"int64"}
UInt64 = @{"uint64"}
Float32 = @{"float32"}
Float64 = @{"float64"}
MacAddress = @{"macaddress"}
DateTime = @{"datetime"}
TypeName = @{ Int8 | UInt8 | Int16 | UInt16 | Int32 | UInt32 | Int64 | UInt64 | Float32 | Float64 | MacAddress | DateTime }

// packet keywords
PacketKeyword = @{ "packet" }
CalculatedKeyword = @{ "calc" }

// expressions
Expr = { Sum }
Sum = { Product ~ ((Plus | Minus) ~ Product)*}
Product = { Power ~ ((Mult | Div) ~ Power)*}
Power = { Value ~ (PowerSign ~ Power)? }
Value = { NumericConstant | Accessor | LParen ~ Expr ~ RParen }

// declaring and accessing variables
Declaration = { Identifier ~ TypeName ~ ArraySpecifier? }
Accessor = {  (SumOf | ProductOf) ~ Identifier ~ !ArraySpecifier | Identifier ~ ArraySpecifier? }
ArraySpecifier = { LArray ~ NumericConstant ~ RArray }

// packet body
Packets = { SOI ~ Packet+ ~ EOI }
Packet = { PacketKeyword ~ Identifier ~ LBracket ~ RuleList ~ RBracket }
RuleList = { Rule ~ (ListSep ~ Rule)* }
Rule = { Declaration | CalculatedField }
CalculatedField = { CalculatedKeyword ~ Identifier ~ Expr }